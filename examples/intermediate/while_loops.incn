"""
While Loops in Incan

This example demonstrates:
- Basic while loop
- While with counter
- While with condition
- break - exit the loop
- continue - skip to next iteration
- Compound assignment: +=, -=, *=, /=, %=
"""

def main() -> None:
    # note: a lot of these examples can be done more efficiently with for loops, these are purely for demo purposes
    mut count = 0
    while count < 5:
        println(f"Count: {count}")
        count += 1
    
    mut total = 0
    mut i = 1
    while i <= 10:
        total += i
        i += 1
    println(f"Sum of 1-10: {total}")
    println("Finding first number whose square > 50:")

    mut n = 1
    while n < 100:
        squared = n * n
        if squared > 50:
            println(f"  Found: {n} (squared={squared})")
            break
        n += 1
    println("Odd numbers from 0-9:")

    mut j = 0
    while j < 10:
        j += 1
        if j % 2 == 0:
            continue
        println(f"  {j}")

    mut x: int = 100
    x -= 10
    x *= 2
    println(f"After compound ops: {x}")
    
    mut y: float = 100.0
    y /= 3
    y %= 7
    println(f"After compound ops: {y}")
    
    mut k = 3
    println("Countdown:")
    while k > 0:
        println(f"  {k}...")
        k -= 1
    println("  Blast off!")
